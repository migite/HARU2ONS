;$V2000G1000S800,600L10000
*define
caption "HARU2NS4ONStest"
nsa
globalon
rmenu "セーブ",save,"ロード",load,"リセット",reset,"スキップ",skip,"ログ",lookback
pretextgosub *pretext_sb

;numalias領域
numalias winmode,100
numalias A_001,101
numalias clearChika,102
numalias clearNagomi,103
numalias clearYuduki,104
numalias clearYuu,105
numalias favNagomi,106
numalias favYuduki,107
numalias favYuu,108

;defsub領域
defsub setwin0
defsub setwin1
defsub setwin2
defsub setwin3
defsub cspchar
defsub csptitle

game
;スプライト番号一覧
;10,11　名前表示
;30~34　立ち絵
;37,38　演出背景(現在廃止中)
;39　標準背景
;41~45　タイトル画面ボタン
;49　タイトル背景

;------------------定義域ここまで-------------------

*start

goto *title

*pretext_sb
saveoff

gettag $0
if %winmode == 1:lsp 10,":s/24,24,0;#ffffff$0",55,425
if %winmode == 1:lsp 11,"sys\namePanel.png",0,417

if %winmode == 2:lsp 10,":s/24,24,0;#ffffff$0",55,50

print 1
saveon
return

*setwin0
textoff
csp 10:csp 11
mov %winmode,0
return

*setwin1
textoff
setwindow3 50,480,32,3,20,20,1,2,10,1,1,":a;sys/msgPanel.png",0,417
mov %winmode,1
return

*setwin2
textoff
setwindow3 100,50,24,10,20,20,1,2,10,1,1,":a;sys/fullPanel.png",0,0
mov %winmode,2
return

*setwin3
textoff
setwindow3 100,50,24,10,20,20,1,2,10,1,1,#000000,0,600,50,500
mov %winmode,3
return

*cspchar
csp 37
csp 38
csp 39
csp 30
csp 31
csp 32
csp 33
csp 34
print 1
return

*csptitle
csp 41
csp 42
csp 43
csp 44
csp 45
csp 49
print 10,300
return

;----------追加命令ここまで-------------

*title
bgm "bgm\bgm_22.ogg"
lsph 41,":a;sys/mainmenuNewGame.png",570,45
lsph 42,":a;sys/mainmenuLoadGame.png",570,90
lsph 43,":a;sys/mainmenuSystem.png",570,135
lsph 44,":a;sys/mainmenuMemories.png",570,180
lsph 45,":a;sys/mainmenuExit.png",570,225
lsph 49,":a;sys/topmenu.png",0,0

vsp 49,1

exbtn 41,41, "P41"
exbtn 42,42, "P42"
exbtn 43,43,"P43"
exbtn 44,44,"P44"
exbtn 45,45,"P45"

exbtn_d "C41C42C43C44C45"

*title_loop

btnwait %0
if %0 == 0 goto *title_loop
if %0 == -1 goto *title_loop

if %0 == 41 goto *startmode
if %0 == 42 goto *loadmode
if %0 == 43 goto *settingmode
if %0 == 44 goto *memory
if %0 == 45 goto *endmode

goto *title_loop
end

*startmode
csptitle
bgmstop
saveon
setwin0
erasetextwindow 0
goto *A_001
end

*loadmode
csptitle
saveon
systemcall load
bgmstop
goto *title
end

*settingmode
setwin1
そんなものはない@
setwin0
goto *title
end

*memory
setwin2
な
い
で
す@
setwin0
goto *title
end

*endmode
csptitle
bg black,10,300
end

;-------------------シナリオここから------------------

