;$V2000G1000S800,600L10000
*define
caption "HARU2NS4ONSVer0.73"
nsa
globalon
rmenu "セーブ",save,"ロード",load,"リセット",reset,"スキップ",skip,"ログ",lookback,"ウインドウを消す",windowerase
pretextgosub *pretext_sb
windowback
humanz 30
savenumber 15
automode

;numalias領域
numalias st_x1,51
numalias st_y1,52
numalias st_x2,53
numalias st_y2,54
numalias st_x3,55
numalias st_y3,57
numalias st_x4,58
numalias st_y4,59
numalias st_x5,60
numalias st_y5,61
numalias x1_default,62
numalias x2_default,63
numalias x3_default,64
numalias x4_default,65
numalias x5_default,66
numalias winmode,100
numalias A_001,101
numalias clearChika,102
numalias clearNagomi,103
numalias clearYuduki,104
numalias clearYuu,105
numalias favNagomi,106
numalias favYuduki,107
numalias favYuu,108
numalias B_004,109
numalias B_008,110
numalias B_011,111
numalias B_015,112
numalias C_007,113
numalias C_012,114
numalias C_015,115
numalias C_017,116
numalias D_004,117
numalias D_006,118
numalias D_007,119
numalias D_009,120
numalias D_013,121
numalias D_015,122
numalias E_007,123
numalias E_008,124
numalias route,125


;defsub領域
defsub setwin0
defsub setwin1
defsub setwin2
defsub setwin3
defsub cspchar
defsub csptitle
defsub stposition

game
;スプライト番号一覧
;10,11　名前表示
;30~34　立ち絵
;37,38　演出背景(現在廃止中)
;39　標準背景
;41~45　タイトル画面ボタン
;49　タイトル背景

;------------------定義域ここまで-------------------

*start

goto *title

*pretext_sb
gettag $0

if %winmode == 0 csp 11

if %winmode == 1 lsp 10,":s/24,24,0;#ffffff$0",55,430
if %winmode == 1 lsp 11,"sys\namePanel.png",0,417,128

if %winmode == 2 lsp 10,":s/24,24,0;#ffffff$1",55,50

print 1

return


*setwin0
textoff
csp 10:csp 11
mov %winmode,0
return

*setwin1
textoff
setwindow3 50,480,32,3,20,20,1,2,10,1,1,":a;sys/msgPanel.png",0,417
mov %winmode,1
return

*setwin2
textoff
setwindow3 100,50,24,10,20,20,1,2,10,1,1,":a;sys/fullPanel.png",0,0
mov %winmode,2
return

*setwin3
textoff
setwindow3 100,50,24,10,20,20,1,2,10,1,1,#000000,0,600,50,500
mov %winmode,3
return

*cspchar
csp 37
csp 38
csp 39
csp 30
csp 31
csp 32
csp 33
csp 34
print 1
return

*csptitle
csp 41
csp 42
csp 43
csp 44
csp 45
csp 49
print 10,300
return

*stposition
getspsize 34,%st_x1,%st_y1
getspsize 33,%st_x2,%st_y2
getspsize 32,%st_x3,%st_y3
getspsize 31,%st_x4,%st_y4
getspsize 30,%st_x5,%st_y5

div %st_x1,2
add %st_x1,%x1_default
sub %st_x1,800
sub %st_y1,600
mov %st_x1,-%st_x1
mov %st_y1,-%st_y1

amsp 34,%st_x1,%st_y1
vsp 34,1

if %st_x2 == 0 print 1
if %st_x2 == 0 return

div %st_x2,2
add %st_x2,%x2_default
sub %st_x2,800
sub %st_y2,600
mov %st_x2,-%st_x2
mov %st_y2,-%st_y2

amsp 33,%st_x2,%st_y2
vsp 33,1

if %st_x3 == 0 print 1
if %st_x3 == 0 return 

div,%st_x3,2
add %st_x3,%x3_default
sub %st_x3,800
sub %st_y3,600
mov %st_x3,-%st_x3
mov %st_y3,-%st_y3

amsp 32,%st_x3,%st_y3
vsp 32,1

if %st_x4 == 0 print 1
if %st_x4 == 0 return

div %st_x4,2
add %st_x4,%x4_default
sub %st_x4,800
sub %st_y4,600
mov %st_x4,-%st_x4
mov %st_y4,-%st_y4

amsp 31,%st_x4,%st_y4
vsp 31,1

if %st_x5 == 0 print 1
if %st_x5 == 0 return

div %st_x5,2
add %st_x5,%x5_default
sub %st_x5,800
sub %st_y5,600
mov %st_x5,-%st_x5
mov %st_y5,-%st_y5

amsp 30,%st_x5,%st_y5
vsp 30,1

return

;----------追加命令ここまで-------------

*title
rmode 0
bgm "bgm\bgm_22.ogg"
lsph 41,":a;sys/mainmenuNewGame.png",570,45
lsph 42,":a;sys/mainmenuLoadGame.png",570,90
lsph 43,":a;sys/mainmenuSystem.png",570,135
lsph 44,":a;sys/mainmenuMemories.png",570,180
lsph 45,":a;sys/mainmenuExit.png",570,225
lsph 49,":a;sys/topmenu.png",0,0

vsp 49,1

exbtn 41,41, "P41"
exbtn 42,42, "P42"
exbtn 43,43,"P43"
exbtn 44,44,"P44"
exbtn 45,45,"P45"

exbtn_d "C41C42C43C44C45"

*title_loop

btnwait %0
if %0 == 0 goto *title_loop
if %0 == -1 goto *title_loop

if %0 == 41 goto *startmode
if %0 == 42 goto *loadmode
if %0 == 43 goto *settingmode
if %0 == 44 goto *memory
if %0 == 45 goto *endmode

goto *title_loop
end

*startmode
csptitle
rmode 1
bgmstop
saveon
setwin0
erasetextwindow 0
goto *A_001
end

*loadmode
csptitle
saveon
lsph 49,":a;sys/saveloadBase.png",0,0:vsp 49,1
print 1
systemcall load
bgmstop
goto *title
end

*settingmode
csptitle
lsph 49,":a;sys/configBase.png",0,0:vsp 49,1
print 1
setwin1
bgmstop
select
"音量設定　マスター１００",*Onryo100,
"音量設定　マスター５０",*Onryo50,
"音量設定　マスター０",*Onryo0

*Onryo100
bgmvol 100
voicevol 100
sevol 100
setwin0
csptitle
goto *title

*Onryo50
bgmvol 50
voicevol 50
sevol 50
setwin0
csptitle
goto *title

*Onryo0
bgmvol 0
voicevol 0
sevol 0
setwin0
csptitle
goto *title

goto *title
end

*Onryoconfig

return

*memory
lsph 49,"sys\exmPrologue.png",0,0:vsp 49,1
print 1
wait 2000
csptitle
goto *title
end

*endmode
csptitle
bg black,10,300
end

;-------------------シナリオここから------------------

